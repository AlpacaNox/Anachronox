#window 4002:8001
startswitch {
  extern deassign l[0] 3
  extern deassign t[0] 3
  extern deassign f[0] 5
  extern deassign B[0] 4
  extern pauselevelmusic
  extern loopsound ../music/mp3/worldskill.mp3
  extern RegisterPic @ym_llung_ex$ /gameflow/yammer/ym_llung_exhale.atd
  extern RegisterPic @ym_rlung_ex$ /gameflow/yammer/ym_rlung_exhale.atd
  extern RegisterPic @ym_llung_in$ /gameflow/yammer/ym_llung_inhale.atd
  extern RegisterPic @ym_rlung_in$ /gameflow/yammer/ym_rlung_inhale.atd
  set anox_worldskill_successful = 0
  extern get_world_skill_level yammer @yammerworldlevel
  if ((@yammerworldlevel == 1) && (ws_skill_level == 2)) {
    set timed_message = 99
    timed_message$="You are not skilled enough to Yammer this person." 
    gosub 1000:1
  }
  extern CreateAPE2DControl push turnup1 442 413 width=20 height=20 nodrawflag=true holdfocusflag=false gamevar=@YMlungchoice returnvalue=1 sequence=4002:8035
  extern CreateAPE2DControl push turnup2 522 413 width=20 height=20 nodrawflag=true holdfocusflag=false gamevar=@YMlungchoice returnvalue=2 sequence=4002:8035
  if (@yammerworldlevel >= 2)
    extern CreateAPE2DControl push turnboth 482 413 width=20 height=20 nodrawflag=true holdfocusflag=false gamevar=@YMlungchoice returnvalue=3 sequence=4002:8035
  extern CreateAPE2DControl push quit 598 191 width=19 height=19 nodrawflag=true holdfocusflag=false gamevar=@GwogIsASlacker returnvalue=1 sequence=4002:8100
  extern CreateAPE2DControl push what 577 191 width=19 height=19 nodrawflag=true holdfocusflag=false gamevar=@YM_NeedHelp returnvalue=1 sequence=4002:8035
  set lastMS = func_GameTime
  set currentMS = lastMS
  set F[1] = 168
  set F[2] = 1
  gosub 4002:8005
}

thinkswitch {
  if (@YammerPause == 1)
    goto 0:0
  if ((func_timer - @YMAirDrainTime) >= 500) {
    set F[1] = (F[1] + 1)
    set F[2] = (F[2] - 1)
    if (F[1] > 168)
      set F[1] = 168
    if (F[2] < 1)
      set F[2] = 1
    set @YMAirDrainTime = func_timer
    set heartState = (1 - heartState)
  }
  if (@FYMASMD == 1)
    if (func_gametime > (@YM_ControlTime + 75)) {
      set @YM_ControlPause = 0
      set @FYMASMD = 0
    }
  if ((func_timer - @YMTimerTime) >= 100) {
    set T[3] = (T[3] - 1)
    set @YMTimerTime = func_timer
    set @YMTimerCountdown = (@YMTimerCountdown - 1)
  }
  if (T[3] < 0) {
    set T[3] = 9
    set T[2] = (T[2] - 1)
  }
  if (T[2] < 0) {
    set T[2] = 9
    set T[1] = (T[1] - 1)
  }
  if (T[1] < 0)
    set T[1] = 0
  if (@YMTimerCountdown <= 0)
    goto 4002:8100
}

style "null"
flags passive2d, TRUE
xpos 364
ypos 190
background "yammer/ym_background.tga" 
if (L[1] == 0)
  background "yammer/ym_llung_exhale.atd" 
if (L[2] == 0)
  image "yammer/ym_rlung_exhale.atd" 128, 0
if (L[1] == 1)
  background "yammer/ym_llung_inhale.atd" 
if (L[2] == 1)
  image "yammer/ym_rlung_inhale.atd" 128, 0
image "yammer/ym_red_bar.tga" 238, F[1], 16, F[2]
image "yammer/ym_larrow_$B[1]$.tga" 79, 224
image "yammer/ym_rarrow_$B[2]$.tga" 159, 224
if (@yammerworldlevel >= 2)
  image "yammer/ym_both_$B[3]$.tga" 119, 224
image "yammer/ym_exit.tga" 235, 2
image "yammer/ym_ask.tga" 214, 2
image "yammer/ym_period.tga" 39, 7
image "yammer/ym_t$t[1]$.tga" 7, 8
image "yammer/ym_t$t[2]$.tga" 27, 8
image "yammer/ym_t$t[3]$.tga" 51, 8

#window 4002:8015
startswitch {
  set @YM_UntilGlowtime = func_gametime
  set @RandomUntilGlowTime = func_drand[150,1000]
  if (@JustStartedYammer == 0) {
    extern FindTexture ym_llung_exhale_handle gameflow/yammer/ym_llung_exhale.atd
    extern FindTexture ym_rlung_exhale_handle gameflow/yammer/ym_rlung_exhale.atd
    extern FindTexture ym_llung_inhale_handle gameflow/yammer/ym_llung_inhale.atd
    extern FindTexture ym_rlung_inhale_handle gameflow/yammer/ym_rlung_inhale.atd
    set @RandomUntilGlowTime = 2500
    set @JustStartedYammer = 1
  }
}

thinkswitch {
  if (@YammerPause)
    goto 0:0
  if (func_gametime > (@YM_UntilGlowtime + @RandomUntilGlowTime)) {
    closewindow 4002:8015
    goto 4002:8020
  }
}

style "null"
flags passive, TRUE

#window 4002:8020
startswitch {
  set @YM_GlowInProgress = 1
  set @YM_GlowDurationTime = func_gametime
  if (ws_difficulty == 1) {
    set @RandomLung = func_drand[1,2]
  } else if (ws_difficulty == 2)
    set @RandomLung = func_drand[1,3]
  if ((ws_difficulty == 2) && (@yammerworldlevel < 2))
    set @RandomLung = 3
  if (@RandomLung == 1) {
    extern SetAnimFrame ym_llung_inhale_handle 0
    set L[1] = 1
    set B[1] = 1
  } else if (@RandomLung == 2) {
    extern SetAnimFrame ym_rlung_inhale_handle 0
    set L[2] = 1
    set B[2] = 1
  } else if (@RandomLung == 3) {
    extern SetAnimFrame ym_llung_inhale_handle 0
    extern SetAnimFrame ym_rlung_inhale_handle 0
    set L[1] = 1
    set L[2] = 1
    set B[3] = 1
  }
  set @RandomReactionTime = func_drand[1500,2500]
}

thinkswitch {
  if (func_gametime > (@YM_GlowDurationTime + @RandomReactionTime)) {
    if ((L[1] == 1) && (L[2] == 0)) {
      extern SetAnimFrame ym_llung_exhale_handle 0
      set L[1] = 0
      set B[1] = 0
    } else if ((L[2] == 1) && (L[1] == 0)) {
      extern SetAnimFrame ym_rlung_exhale_handle 0
      set L[2] = 0
      set B[2] = 0
    } else if ((L[2] == 1) && (L[1] == 1)) {
      extern SetAnimFrame ym_llung_exhale_handle 0
      extern SetAnimFrame ym_rlung_exhale_handle 0
      set L[1] = 0
      set L[2] = 0
      set B[3] = 0
    }
    set @YM_GlowInProgress = 0
    closewindow 4002:8020
    goto 4002:8015
  }
}

style "null"
flags passive, TRUE

#window 4002:9005
title "Grumpos' Yammer"
body "Grumpos needs a lot of air to perform his yammer. You must help him fill his lungs by inhaling at just the right time. "
body "Whenever you see a lung expand, hit the corresponding button as fast and frequently as you can. You can click the arrows with your mouse, or use the arrow keys on your keyboard / keypad. "
startswitch {
  set L[1] = 0
  set L[2] = 0
  set @YammerPause = 1
  closewindow 4002:8015
  closewindow 4002:8020
}

style "style_embrace"
font "anox10black"
flags noscroll, TRUE
flags nograb, TRUE
flags norelease, TRUE
background color1 = ddddddbb color2 = aaaaaaaa color3 = ddddddbb color4 = aaaaaaaa
xpos 30
ypos 55
width 580
nextwindow 4002:9006

#window 4002:9006
title "Grumpos' Yammer"
body "When the lung shrinks, be sure to stop hitting the keys as you will cough and lose a little air each time! Remember air will always be slowly leaking, so don't waste any time. "
finishswitch {
  set @YM_NeedHelp = 0
  set @YammerPause = 0
  goto 4002:8015
}

style "style_embrace"
font "anox10black"
flags noscroll, TRUE
flags nograb, TRUE
flags norelease, TRUE
background color1 = ddddddbb color2 = aaaaaaaa color3 = ddddddbb color4 = aaaaaaaa
xpos 30
ypos 55
width 580

#window 4002:9010
title "Grumpos' Master Yammer"
body "Grumpos needs a lot of air to perform his yammer. You must help him fill his lungs by inhaling at just the right time. "
body "Whenever you see a lung expand, hit the corresponding button as quickly and frequently as you can. You can click the arrows with your mouse, or use the arrow keys on your keyboard/keypad. "
startswitch {
  set L[1] = 0
  set L[2] = 0
  set @YammerPause = 1
  closewindow 4002:8015
  closewindow 4002:8020
}

style "style_embrace"
font "anox10black"
flags noscroll, TRUE
flags nograb, TRUE
flags norelease, TRUE
background color1 = f0f0f0bb color2 = eeeeeecc color3 = f0f0f0bb color4 = eeeeeecc
xpos 30
ypos 55
width 580
nextwindow 4002:9011

#window 4002:9011
title "Grumpos' Master Yammer"
body "When the lung shrinks, be sure to stop hitting the keys or else Grumpos will cough and lose a little air each time! Remember air will always be slowly leaking, so don't waste any time. \n"
style "style_embrace"
font "anox10black"
flags noscroll, TRUE
flags nograb, TRUE
flags norelease, TRUE
background color1 = f0f0f0bb color2 = eeeeeecc color3 = f0f0f0bb color4 = eeeeeecc
xpos 30
ypos 55
width 580
nextwindow 4002:9012

#window 4002:9012
title "Grumpos' Master Yammer"
if (@yammerworldlevel == 1)
  body "Can't keep up with two lungs at once? Maybe Grumpos should look for guidance with his technique.\n"
if (@yammerworldlevel == 2)
  body "Can't keep up with two lungs at once? Try the new double arrow button, or the up or down arrow keys.\n"
finishswitch {
  set @YM_NeedHelp = 0
  set @YammerPause = 0
  goto 4002:8015
}

style "style_embrace"
font "anox10black"
flags noscroll, TRUE
flags nograb, TRUE
flags norelease, TRUE
background color1 = f0f0f0bb color2 = eeeeeecc color3 = f0f0f0bb color4 = eeeeeecc
xpos 30
ypos 55
width 580

#switch 4002:8005
if ((ws_difficulty != 1) && (ws_difficulty != 2))
  set ws_difficulty = 1
if ((ws_time < 1) || (ws_time > 99))
  set ws_time = 35
if (ws_skill_level == 2)
  set ws_difficulty = 2
set @YMTimerTime = func_timer
set @YMTimerCountdown = (ws_time * 10)
set t[1] = func_div[ws_time,10]
set t[2] = func_mod[ws_time,10]
console "pushbind"
if (@yammerworldlevel >= 2)
  console "bind KP_UPARROW \"invoke 4002:8040\""
if (@yammerworldlevel >= 2)
  console "bind KP_DOWNARROW \"invoke 4002:8040\""
if (@yammerworldlevel >= 2)
  console "bind UPARROW \"invoke 4002:8040\""
if (@yammerworldlevel >= 2)
  console "bind DOWNARROW \"invoke 4002:8040\""
console "bind KP_LEFTARROW \"invoke 4002:8045\""
console "bind KP_RIGHTARROW \"invoke 4002:8050\""
console "bind LEFTARROW \"invoke 4002:8045\""
console "bind RIGHTARROW \"invoke 4002:8050\""
if ((ws_skill_level == 1) && (Yammer_First_Time1 == 0)) {
  set Yammer_First_Time1 = 1
  gosub 4002:9000
}
if ((ws_skill_level == 2) && (Yammer_First_Time2 == 0)) {
  set Yammer_First_Time2 = 1
  gosub 4002:9000
}
goto 4002:8015

#switch 4002:8035
if (@YammerPause == 1)
  goto 0:0
if (@YM_ControlPause == 1)
  goto 0:0
if (@YM_NeedHelp == 1)
  goto 4002:9000
if (@YM_GlowInProgress == 0)
  goto 4002:8030
if (@YMlungchoice == @RandomLung) {
  goto 4002:8025
} else
  goto 4002:8030
goto 0:0

#switch 4002:8040
if (@yammerworldlevel < 2)
  goto 0:0
set @YMlungchoice = 3
goto 4002:8035

#switch 4002:8045
set @YMlungchoice = 1
goto 4002:8035

#switch 4002:8050
set @YMlungchoice = 2
goto 4002:8035

#switch 4002:8025
if (@YammerPause == 1)
  goto 0:0
extern playsound "ui/gen/new/okay2.wav"
set F[1] = (F[1] - 3)
set F[2] = (F[2] + 3)
set @YM_ControlTime = func_gametime
set @YM_ControlPause = 1
set @FYMASMD = 1
set @YMlungchoice = 0
if (F[2] >= 130)
  goto 4002:8095
goto 0:0

#switch 4002:8030
if (@YammerPause == 1)
  goto 0:0
extern playsound "ui/gen/new/bad_selection4.wav"
set F[1] = (F[1] + 3)
set F[2] = (F[2] - 3)
if (F[1] > 168)
  set F[1] = 168
if (F[2] < 1)
  set F[2] = 1
set @YM_ControlTime = func_gametime
set @YM_ControlPause = 1
set @FYMASMD = 1
set @YMlungchoice = 0
goto 0:0

#switch 4002:8095
set anox_worldskill_successful = 1
extern playsound "ui/gen/ui_gen_chng_good.wav"
goto 4002:8100

#switch 4002:8100
if (@YammerPause)
  goto 0:0
extern stopsound ../music/mp3/worldskill.mp3
extern resumelevelmusic
closewindow 4002:8015
closewindow 4002:8020
closewindow 4002:8001
extern RemoveAPE2DControl turnup1
extern RemoveAPE2DControl turnup2
extern RemoveAPE2DControl turnboth
extern RemoveAPE2DControl quit
extern RemoveAPE2DControl what
if (anox_worldskill_successful == 0)
  extern playsound "ui/gen/ui_gen_quit.wav"
console "popbind"
extern deassign l[0] 3
extern deassign t[0] 3
extern deassign f[0] 5
extern deassign B[0] 4
unset @YMlungchoice
unset @RandomLung
unset @YM_GlowInProgress
unset @YM_TimeToSpare
unset @JustStartedYammer
unset @YM_GlowDurationTime
unset @RandomReactionTime
unset @RandomUntilGlowTime
unset @RandomLung
unset ws_time
unset ws_difficulty
unset ws_doing_wskill

#switch 4002:9000
set B[1] = 0
set B[2] = 0
set B[3] = 0
if (ws_skill_level == 1)
  goto 4002:9005 
if (ws_skill_level == 2)
  goto 4002:9010 
goto 0:0
